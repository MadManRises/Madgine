#include "scene.sl"

#include "light.hlsl"

cbuffer PerApplication : register(b0)
{
    ScenePerApplication app;
}

cbuffer PerFrame : register(b1)
{   
    ScenePerFrame frame;
}

cbuffer PerObject : register(b2)
{
    ScenePerObject object;
}

cbuffer Bones : register(b3)
{
    matrix bones[32];
}

struct AppData {
#ifdef HAS_POS_3D
    float3 aPos : POSITION0;
#endif
#ifdef HAS_POS_4D
    float4 aPos : POSITION0;
#endif
#ifdef HAS_POS2
    float2 aPos2 : POSITION1;
#endif
#ifdef HAS_NORMAL
    float3 aNormal : NORMAL;
#endif
#ifdef HAS_COLOR
    float4 aColor : COLOR;
#endif
#ifdef HAS_UV
    float2 aUV : TEXCOORD;
#endif
#ifdef HAS_BONE_INDICES
	int4 aBoneIDs : BONEINDICES;
#endif
#ifdef HAS_BONE_WEIGHTS
	float4 aWeights : WEIGHTS;
#endif
    SceneInstanceData aInstance : INSTANCEDATA;
};

struct VertexShaderOutput {
    float4 position : SV_POSITION;
    float4 color : COLOR;
    float4 worldPos : POSITION;
    float3 normal : NORMAL;
    float2 uv : TEXCOORD0;
    float4 lightViewPosition : TEXCOORD1;
};

VertexShaderOutput main(AppData IN)
{
    VertexShaderOutput OUT;

#ifdef HAS_POS_3D
    float4 pos = float4(IN.aPos, 1);
#else
    #ifdef HAS_POS_4D
        float4 pos = IN.aPos;
    #else
        float4 pos = float4(0,0,0,1);
    #endif
#endif

#ifdef HAS_POS2
    float2 aPos2 = IN.aPos2;
#else
    float2 aPos2 = float2(0,0);
#endif

#ifdef HAS_BONE_INDICES
    if (object.hasSkeleton) {
	    matrix BoneTransform = bones[IN.aBoneIDs[0]] * IN.aWeights[0]
	    + bones[IN.aBoneIDs[1]] * IN.aWeights[1]
	    + bones[IN.aBoneIDs[2]] * IN.aWeights[2]
	    + bones[IN.aBoneIDs[3]] * IN.aWeights[3];
	    OUT.worldPos = mul(IN.aInstance.m, mul(BoneTransform, pos));
    } else {
        OUT.worldPos = mul(IN.aInstance.m, pos);
    }
#else
	OUT.worldPos = mul(IN.aInstance.m, pos);
#endif
    
    OUT.position = mul(app.p, mul(frame.v, OUT.worldPos) + float4(aPos2, 0.0, 0.0));
    OUT.position.z += OUT.position.w;
    OUT.position.z /= 2;
    
#ifdef HAS_COLOR
    OUT.color = IN.aColor;
#else
    OUT.color = float4(1,1,1,1);
#endif
#ifdef HAS_NORMAL
	OUT.normal = mul((float3x3)IN.aInstance.anti_m, IN.aNormal);
#else
    OUT.normal = float3(0,0,0);
#endif
#ifdef HAS_UV
	OUT.uv = IN.aUV;
#else
    OUT.uv = float2(0,0);
#endif

    OUT.lightViewPosition = projectShadow(frame.light.caster, OUT.worldPos);
 
    return OUT;
}
